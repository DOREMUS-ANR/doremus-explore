PREFIX mus: <http://data.doremus.org/ontology#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX geonames: <http://www.geonames.org/ontology#>

SELECT DISTINCT *
WHERE {
  VALUES ?class {mus:M42_Performed_Expression_Creation efrbroo:F30_Publication_Event}
  VALUES ?expression { %%uri%% }

  ?event a ?class;
        efrbroo:R24_created | efrbroo:R17_created ?realizedExpression .
  ?realizedExpression ecrm:P165_incorporates | mus:U54_is_performed_expression_of ?expression .

  BIND( EXISTS { [] mus:U5_had_premiere / ecrm:P9_consists_of ?event } as ?isPremiere )
  BIND( EXISTS { [] mus:U4_had_princeps_publication ?event } as ?isPrincepsPub )

  OPTIONAL { ?event ecrm:P3_has_note ?note. }

  OPTIONAL { ?realizedExpression rdfs:label ?title . }

  OPTIONAL { ?event ecrm:P4_has_time-span / time:hasBeginning / time:inXSDDate ?time . }
  OPTIONAL { ?event ecrm:P7_took_place_at ?placeURI
    OPTIONAL { ?placeURI ecrm:P87_is_identified_by | rdfs:label | geonames:name ?place }
  }
  OPTIONAL {
    ?event ecrm:P9_consists_of ?activity .
    ?activity ecrm:P14_carried_out_by ?actor .
    OPTIONAL {
    { SELECT ?actor sql:BEST_LANGMATCH(?actorName, '%%lang%%', 'en') as ?actorName
      WHERE { ?actor rdfs:label ?actorName }
    } }
    OPTIONAL { ?activity mus:U35_foresees_function ?function }
    OPTIONAL {
      ?activity mus:U1_used_medium_of_performance ?mopURI
      OPTIONAL {{ SELECT ?mopURI sql:BEST_LANGMATCH(?mop, '%%lang%%', 'en') as ?mop
        WHERE { ?mopURI skos:prefLabel | ecrm:P1_is_identified_by ?mop }
      }}
    }
  }
}
