PREFIX mus: <http://data.doremus.org/ontology#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>

SELECT DISTINCT *
FROM <http://data.doremus.org/>
WHERE {
    %%uri%% a efrbroo:F22_Self-Contained_Expression ;
             ecrm:P102_has_title ?title .
    FILTER(str(?title)!="")
    OPTIONAL{%%uri%% efrbroo:R45i_was_assigned_by ?assigned .}
	  OPTIONAL{%%uri%% ecrm:P3_has_note ?note .}
    OPTIONAL{%%uri%% mus:U13_has_intended_casting ?casting .
        FILTER(!regex(?casting, "node"))
    }
    OPTIONAL{%%uri%% mus:U13_has_intended_casting ?castingN .
             ?castingN ecrm:P3_has_note ?castingNote .}
    OPTIONAL{%%uri%% efrbroo:R17i_was_created_by ?created .}
    OPTIONAL{%%uri%% mus:U12_has_genre ?genre .
            FILTER isURI(?genre)
    }
    OPTIONAL{%%uri%% mus:U12_has_genre ?genreN .
             ?genreN ecrm:P1_is_identified_by ?genreID .}
    OPTIONAL{%%uri%% efrbroo:R40i_is_representative_expression_of ?repExp .}
    OPTIONAL{
			%%uri%% mus:U17_has_opus_statement ?opus .
			OPTIONAL{?opus ecrm:P3_has_note ?opusNote .}
			OPTIONAL{?opus ecrm:P106_is_composed_of ?opusComp .}
      FILTER(bound(?opusComp) || bound(?opusNote))
	  }
    OPTIONAL{%%uri%% mus:U10_has_order_number ?order .}
    OPTIONAL{%%uri%% mus:U11_has_key ?key .
            FILTER isURI(?key)
    }
    OPTIONAL{%%uri%% mus:U11_has_key ?keyN .
             ?keyN ecrm:P1_is_identified_by ?keyID .}
    OPTIONAL{
			%%uri%% ecrm:P67_refers_to ?refers .
			OPTIONAL{?refers ecrm:P3_has_note ?refersNote .}
	  }
    OPTIONAL{
  		%%uri%% mus:U16_has_catalogue_statement ?catalogue .
  		OPTIONAL{?catalogue ecrm:P3_has_note ?catagNote .}
  		OPTIONAL{?catalogue ecrm:P106_is_composed_of ?catagComp .}
	  }
    OPTIONAL{%%uri%% ecrm:P165i_is_incorporated_in ?incorporated .}
    OPTIONAL{%%uri%% ecrm:P1_is_identified_by ?identified .}
    OPTIONAL{?complexWork efrbroo:R40_has_representative_expression %%uri%% .}
    OPTIONAL{?individualWork efrbroo:R9_is_realised_in %%uri%% .}
    OPTIONAL{?expCreation efrbroo:R17_created %%uri%% ;
             ecrm:P9_consists_of ?activity .
             ?activity ecrm:P14_carried_out_by ?person .
             ?person ecrm:P1_is_identified_by ?composer .}
}
