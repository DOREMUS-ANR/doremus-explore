PREFIX mus: <http://data.doremus.org/ontology/>
PREFIX cidoc: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX frbroo: <http://erlangen-crm.org/efrbroo/>

SELECT * from <http://data.doremus.org/data>
WHERE {
    $$expr$$ a frbroo:F22_Self-Contained_Expression ;
             cidoc:P102_has_title ?title .
    OPTIONAL{$$expr$$ frbroo:R45i_was_assigned_by ?assigned .}
	  OPTIONAL{$$expr$$ cidoc:P3_has_note ?note .}
    OPTIONAL{$$expr$$ mus:U13_has_casting ?casting .
			OPTIONAL{?casting cidoc:P3_has_note ?castingNote .}
	  }
    OPTIONAL{$$expr$$ frbroo:R17i_was_created_by ?created .}
    OPTIONAL{
			$$expr$$ mus:U12_has_genre ?genre .
			OPTIONAL{?genre cidoc:P1_is_identified_by ?genreID .}
	  }
    OPTIONAL{$$expr$$ frbroo:R40i_is_representative_expression_of ?repExp .}
    OPTIONAL{
			$$expr$$ mus:U17_has_opus_statement ?opus .
			OPTIONAL{?opus cidoc:P3_has_note ?opusNote .}
			OPTIONAL{?opus cidoc:P106_is_composed_of ?opusComp .}
      FILTER(bound(?opusComp) || bound(?opusNote))
	  }
    OPTIONAL{$$expr$$ mus:U10_has_order_number ?order .}
    OPTIONAL{
			$$expr$$ mus:U11_has_key ?key .
			OPTIONAL{?key cidoc:P1_is_identified_by ?keyID .}
	  }
    OPTIONAL{
			$$expr$$ cidoc:P67_refers_to ?refers .
			OPTIONAL{?refers cidoc:P3_has_note ?refersNote .}
	  }
    OPTIONAL{
  		$$expr$$ mus:U16_has_catalogue_statement ?catalogue .
  		OPTIONAL{?catalogue cidoc:P3_has_note ?catagNote .}
  		OPTIONAL{?catalogue cidoc:P106_is_composed_of ?catagComp .}
	  }
    OPTIONAL{$$expr$$ cidoc:P165i_is_incorporated_in ?incorporated .}
    OPTIONAL{$$expr$$ cidoc:P1_is_identified_by ?identified .}
    OPTIONAL{?complexWork frbroo:R40_has_representative_expression $$expr$$ .}
    OPTIONAL{?individualWork frbroo:R9_is_realised_in $$expr$$ .}
    OPTIONAL{?expCreation frbroo:R17_created $$expr$$ ;
             cidoc:P9_consists_of ?activity .
             ?activity cidoc:P14_carried_out_by ?person .
             ?person cidoc:P131_is_identified_by ?composer .}
}
