PREFIX mus: <http://data.doremus.org/ontology#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>


SELECT
  ?expression
  sql:BEST_LANGMATCH(?title, '%%lang%%, en;q=0.9, *;q=0.1', 'en') AS ?title
  sql:BEST_LANGMATCH(?composer, '%%lang%%, en;q=0.9, *;q=0.1', 'en') AS ?composer
  (SAMPLE(?catalogue) AS ?catalogue)
WHERE {
    %%uri%% a efrbroo:F22_Self-Contained_Expression ;
       mus:U13_has_casting / mus:U23_has_casting_detail / mus:U2_foresees_use_of_medium_of_performance_of_type ?mop.

    ?expression a efrbroo:F22_Self-Contained_Expression ;
            mus:U13_has_casting / mus:U23_has_casting_detail / mus:U2_foresees_use_of_medium_of_performance_of_type ?mop;
            mus:U70_has_title ?title .
    OPTIONAL { ?expression mus:U16_has_catalogue_statement / rdfs:label ?catalogue }

    OPTIONAL{
      ?expCreation efrbroo:R17_created ?expression ;
                  ecrm:P9_consists_of / ecrm:P14_carried_out_by / foaf:name ?composer .
    }
    FILTER( ?expression != %%uri%%)
}
GROUP BY ?expression
ORDER BY desc(COUNT(DISTINCT ?mop))
LIMIT %%limit%%
